(ns vybe.sdf.screenshot
  (:require
   [vybe.util :as u]
   ["vulkan/sdf_engine.hpp" :as sdfx :scope "sdfx"]
   ["vulkan/vulkan.h" :as vk :scope ""]
   ["vulkan/stb_image_write.h" :as stb :scope ""]))

;; ============================================================================
;; Screenshot - jank implementation with boxing for Vulkan handles
;; ============================================================================

;; alloc_screenshot_cmd stays in C++ because jank can't easily create a local
;; VkCommandBuffer variable (VkCommandBuffer is already a pointer typedef)
(defn alloc-cmd-buffer []
  (cpp/box (sdfx/alloc_screenshot_cmd)))

(defn free-cmd-buffer! [cmd]
  (let [vk-cmd (cpp/unbox (cpp/type "struct VkCommandBuffer_T*") cmd)
        cmd-ptr (cpp/& vk-cmd)]
    (vk/vkFreeCommandBuffers (sdfx/get_device) (sdfx/get_command_pool)
                             (cpp/uint32_t. 1) cmd-ptr)))

(defn submit-and-wait! [cmd]
  (let [vk-cmd (cpp/unbox (cpp/type "struct VkCommandBuffer_T*") cmd)
        cmd-ptr (cpp/cast (cpp/type "VkCommandBuffer const*") (cpp/& vk-cmd))
        queue (sdfx/get_graphics_queue)
        info (cpp/VkSubmitInfo.)]
    (u/merge* info {:sType vk/VK_STRUCTURE_TYPE_SUBMIT_INFO
                    :commandBufferCount (cpp/uint32_t. 1)
                    :pCommandBuffers cmd-ptr})
    (vk/vkQueueSubmit queue (cpp/uint32_t. 1) (cpp/& info) cpp/VK_NULL_HANDLE)
    (vk/vkQueueWaitIdle queue)))

(defn begin-cmd-buffer! [cmd]
  (let [info (cpp/VkCommandBufferBeginInfo.)]
    (u/merge* info {:sType vk/VK_STRUCTURE_TYPE_COMMAND_BUFFER_BEGIN_INFO
                    :flags (cpp/VkCommandBufferUsageFlags. vk/VK_COMMAND_BUFFER_USAGE_ONE_TIME_SUBMIT_BIT)})
    (vk/vkBeginCommandBuffer (cpp/unbox (cpp/type "struct VkCommandBuffer_T*") cmd) (cpp/& info))))

(defn end-cmd-buffer! [cmd]
  (vk/vkEndCommandBuffer (cpp/unbox (cpp/type "struct VkCommandBuffer_T*") cmd)))

(defn barrier-to-general! "Pipeline barrier: UNDEFINED -> GENERAL (for compute write)"
  [cmd image]
  (let [vk-cmd (cpp/unbox (cpp/type "struct VkCommandBuffer_T*") cmd)
        vk-image (cpp/unbox (cpp/type "struct VkImage_T*") image)
        barrier (cpp/VkImageMemoryBarrier.)]
    (u/merge* barrier {:sType vk/VK_STRUCTURE_TYPE_IMAGE_MEMORY_BARRIER
                       :oldLayout vk/VK_IMAGE_LAYOUT_UNDEFINED
                       :newLayout vk/VK_IMAGE_LAYOUT_GENERAL
                       :srcQueueFamilyIndex vk/VK_QUEUE_FAMILY_IGNORED
                       :dstQueueFamilyIndex vk/VK_QUEUE_FAMILY_IGNORED
                       :image vk-image
                       :subresourceRange.aspectMask (cpp/VkImageAspectFlags. vk/VK_IMAGE_ASPECT_COLOR_BIT)
                       :subresourceRange.baseMipLevel (cpp/uint32_t 0)
                       :subresourceRange.levelCount (cpp/uint32_t 1)
                       :subresourceRange.baseArrayLayer (cpp/uint32_t 0)
                       :subresourceRange.layerCount (cpp/uint32_t 1)
                       :srcAccessMask (cpp/VkAccessFlags. 0)
                       :dstAccessMask (cpp/VkAccessFlags. vk/VK_ACCESS_SHADER_WRITE_BIT)})
    (vk/vkCmdPipelineBarrier vk-cmd
                             vk/VK_PIPELINE_STAGE_TOP_OF_PIPE_BIT
                             vk/VK_PIPELINE_STAGE_COMPUTE_SHADER_BIT
                             (cpp/VkDependencyFlags. 0)
                             (cpp/uint32_t 0) (cpp/cast (cpp/type "VkMemoryBarrier const*") cpp/nullptr)
                             (cpp/uint32_t 0) (cpp/cast (cpp/type "VkBufferMemoryBarrier const*") cpp/nullptr)
                             (cpp/uint32_t 1) (cpp/& barrier))))

(defn barrier-to-transfer-src! "Pipeline barrier: GENERAL -> TRANSFER_SRC (after compute, for copy)"
  [cmd image]
  (let [vk-cmd (cpp/unbox (cpp/type "struct VkCommandBuffer_T*") cmd)
        vk-image (cpp/unbox (cpp/type "struct VkImage_T*") image)
        barrier (cpp/VkImageMemoryBarrier.)]
    (u/merge* barrier {:sType vk/VK_STRUCTURE_TYPE_IMAGE_MEMORY_BARRIER
                       :oldLayout vk/VK_IMAGE_LAYOUT_GENERAL
                       :newLayout vk/VK_IMAGE_LAYOUT_TRANSFER_SRC_OPTIMAL
                       :srcQueueFamilyIndex vk/VK_QUEUE_FAMILY_IGNORED
                       :dstQueueFamilyIndex vk/VK_QUEUE_FAMILY_IGNORED
                       :image vk-image
                       :subresourceRange.aspectMask (cpp/VkImageAspectFlags. vk/VK_IMAGE_ASPECT_COLOR_BIT)
                       :subresourceRange.baseMipLevel (cpp/uint32_t 0)
                       :subresourceRange.levelCount (cpp/uint32_t 1)
                       :subresourceRange.baseArrayLayer (cpp/uint32_t 0)
                       :subresourceRange.layerCount (cpp/uint32_t 1)
                       :srcAccessMask (cpp/VkAccessFlags. vk/VK_ACCESS_SHADER_WRITE_BIT)
                       :dstAccessMask (cpp/VkAccessFlags. vk/VK_ACCESS_TRANSFER_READ_BIT)})
    (vk/vkCmdPipelineBarrier vk-cmd
                             vk/VK_PIPELINE_STAGE_COMPUTE_SHADER_BIT
                             vk/VK_PIPELINE_STAGE_TRANSFER_BIT
                             (cpp/VkDependencyFlags. 0)
                             (cpp/uint32_t 0) (cpp/cast (cpp/type "VkMemoryBarrier const*") cpp/nullptr)
                             (cpp/uint32_t 0) (cpp/cast (cpp/type "VkBufferMemoryBarrier const*") cpp/nullptr)
                             (cpp/uint32_t 1) (cpp/& barrier))))

(defn dispatch-compute! [cmd]
  (let [vk-cmd (cpp/unbox (cpp/type "struct VkCommandBuffer_T*") cmd)
        width (sdfx/get_swapchain_width)
        height (sdfx/get_swapchain_height)
        desc-set (sdfx/get_descriptor_set)
        desc-ptr (cpp/& desc-set)]
    (vk/vkCmdBindPipeline vk-cmd vk/VK_PIPELINE_BIND_POINT_COMPUTE (sdfx/get_compute_pipeline))
    (vk/vkCmdBindDescriptorSets vk-cmd vk/VK_PIPELINE_BIND_POINT_COMPUTE
                                (sdfx/get_compute_pipeline_layout)
                                (cpp/uint32_t 0) (cpp/uint32_t 1) desc-ptr
                                (cpp/uint32_t 0) (cpp/cast (cpp/type "uint32_t const*") cpp/nullptr))
    (vk/vkCmdDispatch vk-cmd
                      (cpp/uint32_t (/ (+ width 15) 16))
                      (cpp/uint32_t (/ (+ height 15) 16))
                      (cpp/uint32_t 1))))

;; create_screenshot_buffer stays in C++ because Vulkan handle types (VkBuffer, VkDeviceMemory)
;; are pointer typedefs, making it difficult to create local variables for output params in jank
(defn create-staging-buffer [size]
  (let [result (sdfx/create_screenshot_buffer (cpp/VkDeviceSize. size))]
    (when (cpp/.-success result)
      {:buffer (cpp/box (cpp/.-buffer result))
       :memory (cpp/box (cpp/.-memory result))})))

(defn copy-image-to-buffer! [cmd image buffer width height]
  (let [vk-cmd (cpp/unbox (cpp/type "struct VkCommandBuffer_T*") cmd)
        vk-image (cpp/unbox (cpp/type "struct VkImage_T*") image)
        vk-buffer (cpp/unbox (cpp/type "struct VkBuffer_T*") buffer)
        region (cpp/VkBufferImageCopy.)]
    (u/merge* region {:bufferOffset (cpp/VkDeviceSize. 0)
                      :bufferRowLength (cpp/uint32_t. 0)
                      :bufferImageHeight (cpp/uint32_t. 0)
                      :imageSubresource.aspectMask (cpp/VkImageAspectFlags. vk/VK_IMAGE_ASPECT_COLOR_BIT)
                      :imageSubresource.mipLevel (cpp/uint32_t. 0)
                      :imageSubresource.baseArrayLayer (cpp/uint32_t. 0)
                      :imageSubresource.layerCount (cpp/uint32_t. 1)
                      :imageOffset.x (cpp/int32_t. 0)
                      :imageOffset.y (cpp/int32_t. 0)
                      :imageOffset.z (cpp/int32_t. 0)
                      :imageExtent.width (cpp/uint32_t. width)
                      :imageExtent.height (cpp/uint32_t. height)
                      :imageExtent.depth (cpp/uint32_t. 1)})
    (vk/vkCmdCopyImageToBuffer vk-cmd vk-image
                               vk/VK_IMAGE_LAYOUT_TRANSFER_SRC_OPTIMAL
                               vk-buffer (cpp/uint32_t. 1) (cpp/& region))))

(defn map-memory [memory size]
  (let [vk-memory (cpp/unbox (cpp/type "struct VkDeviceMemory_T*") memory)
        data (cpp/new (cpp/type "void*"))]
    (vk/vkMapMemory (sdfx/get_device) vk-memory
                    (cpp/VkDeviceSize. 0) (cpp/VkDeviceSize. size)
                    (cpp/VkMemoryMapFlags. 0) data)
    (cpp/box (cpp/* data))))

(defn unmap-memory! [memory]
  (vk/vkUnmapMemory (sdfx/get_device) (cpp/unbox (cpp/type "struct VkDeviceMemory_T*") memory)))

(defn destroy-staging-buffer! [buffer memory]
  (let [device (sdfx/get_device)
        vk-buffer (cpp/unbox (cpp/type "struct VkBuffer_T*") buffer)
        vk-memory (cpp/unbox (cpp/type "struct VkDeviceMemory_T*") memory)]
    (vk/vkDestroyBuffer device vk-buffer cpp/nullptr)
    (vk/vkFreeMemory device vk-memory cpp/nullptr)))

;; Use C++ helper for PNG writing (jank GC can't handle 230k loop iterations)
(defn write-png-downsampled [filepath pixels width height scale]
  (sdfx/write_png_downsampled filepath
                              (cpp/unbox (cpp/type "void*") pixels)
                              (cpp/uint32_t width)
                              (cpp/uint32_t height)
                              (cpp/uint32_t scale)))

(defn save-screenshot!
  "Capture current frame and save to PNG file."
  [filepath]
  (if (not (sdfx/engine_initialized))
    (do
      (println "Screenshot failed - engine not initialized")
      false)
    (let [width (sdfx/get_swapchain_width)
          height (sdfx/get_swapchain_height)
          image-size (* width height 4)
          image (cpp/box (sdfx/get_compute_image))
          cmd (alloc-cmd-buffer)
          _ (do (begin-cmd-buffer! cmd)
                (barrier-to-general! cmd image)
                (dispatch-compute! cmd)
                (barrier-to-transfer-src! cmd image))
          staging (create-staging-buffer image-size)
          buffer (:buffer staging)
          memory (:memory staging)
          _ (do (copy-image-to-buffer! cmd image buffer width height)
                (end-cmd-buffer! cmd)
                (submit-and-wait! cmd))
          pixels (map-memory memory image-size)
          scale 4
          result (write-png-downsampled filepath pixels width height scale)
          _ (do (unmap-memory! memory)
                (destroy-staging-buffer! buffer memory)
                (free-cmd-buffer! cmd))]
      (if (> result 0)
        (do
          (println "Screenshot saved to" filepath (str "(" (/ width scale) "x" (/ height scale) ")"))
          true)
        (do
          (println "Failed to write PNG:" filepath)
          false)))))
#_ (save-screenshot! "eita.png")
